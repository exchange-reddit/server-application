plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
}


group = 'com.omniversity'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

ext {
	grpcVersion = "1.70.0"
	protobufJavaVersion = "3.25.6"
	springGrpcVersion = "0.5.0"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'net.devh:grpc-spring-boot-starter:3.0.0.RELEASE'

	// Updated annotation API
	implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'

	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	testImplementation 'org.springframework.grpc:spring-grpc-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Protobuf Java
	implementation "com.google.protobuf:protobuf-java:${protobufJavaVersion}"

	// javax annotation
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	// swagger ui
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufJavaVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {
				outputSubDir = 'grpc' // Optional: Organize generated gRPC files
			}
		}
		all()*.builtins {
			java {
				outputSubDir = 'java' // Optional: Organize generated Protobuf files
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'src/main/java', "${protobuf.generatedFilesBaseDir}/main/grpc", "${protobuf.generatedFilesBaseDir}/main/java"
		}
	}
}

